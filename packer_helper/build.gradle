apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group = 'com.nier.packer'
version = '1.0'

buildscript {
    def versionConfig = new ConfigSlurper().parse(new File("./../version.properties").toURI().toURL())
    ext {
        kotlin_version = versionConfig.kotlin
        anko_version = versionConfig.anko
        gradle_version = versionConfig.android_gradle_plugin
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    google()
}

sourceSets {
    main.java.srcDirs += "src/main/java"
    main.kotlin.srcDirs += "src/main/kotlin"
}

task customClean(type: Delete) {
    delete "${project.projectDir}${File.separator}tmpNierDebug.apk",
            "${project.projectDir}${File.separator}tmpFgdRelease.apk"
}

clean.dependsOn(customClean)

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}


dependencies {
    implementation localGroovy()
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"
    implementation "com.android.tools.build:apksig:3.4.1"
    implementation 'com.google.code.gson:gson:2.8.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'helper'
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven { url = uri("../publish") }
    }
}
